managed implementation in class zbp_dd_r_incident_bam unique;
strict ( 2 );
with draft;

define behavior for ZDD_R_INCIDENT_BAM alias Incident
persistent table zdt_inct_bam
draft table zdt_inct_d_bam
lock master
total etag LastChangedAt
etag master LocalLastChangedAt
authorization master ( global, instance )
{
  create;
  update;
  delete;
  //Composición del historial
  association _History { create; with draft; }

  //Campos de solo lectura y autoincremental
  field ( numbering : managed, readonly ) IncUuid;
  field ( readonly ) IncidentId,
  Status,
  ChangedDate,
  LocalCreatedAt,
  LocalCreatedBy,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;

  //Campos obligatorios
  field ( mandatory ) Title, Description, Priority;

  //  //Caracteristica dinamica sobre status para validar si se pueden cambiar o no
  //  field ( features : instance ) Status;

  //definición accion botón status
  action ( features : instance, authorization : update ) ChangeStatus
    parameter ZAE_STATUS_BAM result [1] $self;

  //accion interna para refrescar en la pantalla el cambio del estatus
  //  internal action defineNewStatus;

  validation validateMandatory on save { create; field Title, Description, Priority, Status, CreationDate; }
  validation validateStatusChange on save { create; field Status; }
  validation validateDates on save { create; field CreationDate, ChangedDate; }


  //NUEVAS validaciones solicitadas
  //1) Si cambia a IP, debe tener Responsible
  //validation validate_responsible_on_ip on save { field Status, Responsible; }

  //2) Solo Responsible o Admin pueden cambiar Status
  //validation validate_who_can_change_status on save { field Status, Responsible; }

  determination setInitialValues on modify { create; }
  determination setInitialHistory on save { create; }

  determine action validateMandatoryFields { validation validateMandatory; }
  determine action validateStatusChangeFields { validation validateStatusChange; }
  determine action validateDatesFields { validation validateDates; }

  side effects
  {
    determine action validateMandatoryFields executed on field Title, field Description, field Priority,
        field Status, field CreationDate affects messages;
    determine action validateStatusChangeFields executed on field Status affects messages;
    determine action validateDatesFields executed on field CreationDate, field ChangedDate affects messages;
  }

  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;

  draft determine action Prepare
  {
    validation validateMandatory;
    validation validateStatusChange;
    validation validateDates;
  }

  mapping for zdt_inct_bam
    {
      IncUuid            = inc_uuid;
      IncidentId         = incident_id;
      Title              = title;
      Description        = description;
      Status             = status;
      Priority           = priority;
      CreationDate       = creation_date;
      ChangedDate        = changed_date;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}


define behavior for ZDD_INCIDENT_H_BAM alias History
persistent table zdt_inct_h_bam
draft table zdt_inct_h_d_bam
lock dependent by _Incident
etag master LocalLastChangedAt
authorization dependent by _Incident
{
  update;
  delete;
  association _Incident { with draft; }

  field ( numbering : managed, readonly ) HisUuid;

  field ( readonly ) IncUuid,
  LocalCreatedBy,
  LocalCreatedAt,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;

  mapping for zdt_inct_h_bam
    {
      HisUuid            = his_uuid;
      IncUuid            = inc_uuid;
      HisId              = his_id;
      PreviousStatus     = previous_status;
      NewStatus          = new_status;
      Text               = text;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}