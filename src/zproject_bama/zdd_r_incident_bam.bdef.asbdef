managed implementation in class zbp_dd_r_incident_bam unique;
strict ( 2 );
with draft;

define behavior for ZDD_R_INCIDENT_BAM alias Incident
persistent table zdt_inct_bam
draft table zdt_inct_d_bam
etag master LocalLastChangedAt
lock master total etag LastChangedAt
authorization master ( global, instance )
{
  create;
  update;
  delete;

  association _History { create ( features : instance ); with draft; }

  field ( readonly ) IncidentId,
  Status,
  ChangedDate,
  LocalCreatedAt,
  LocalCreatedBy,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;

  field ( numbering : managed, readonly ) IncUuid;

  field ( mandatory ) Title, Description, Priority;

  action ( features : instance, authorization : update ) ChangeStatus
    parameter ZAE_STATUS_BAM result [1] $self;

  side effects { action ChangeStatus affects $self; }

  internal action setHistory;

  validation validatePriority on save { create; update; field Priority; }
  validation validateDates on save { create; field CreationDate; }

  //  validation validateCreation on save { create; field CreationDate; }
  //  validation validateUpdate on save { update; field ChangedDate; }

  determination setInitialHistory on save { create; }
  determination setInitialValues on modify { create; }


  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;
  draft determine action Prepare
  {
    validation validatePriority;
    validation validateDates;

    //    validation validateCreation;
    //    validation validateUpdate;
  }

  mapping for zdt_inct_bam
    {
      IncUuid            = inc_uuid;
      IncidentId         = incident_id;
      Title              = title;
      Description        = description;
      Status             = status;
      Priority           = priority;
      CreationDate       = creation_date;
      ChangedDate        = changed_date;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}


define behavior for ZDD_INCIDENT_H_BAM alias History
implementation in class zbp_dd_incident_h_bam unique
persistent table zdt_inct_h_bam
draft table zdt_inct_h_d_bam
lock dependent by _Incident
etag master LocalLastChangedAt
authorization dependent by _Incident
{
  update;
  delete;

  association _Incident { with draft; }

  field ( numbering : managed, readonly ) HisUuid;

  field ( readonly ) IncUuid,
  LocalCreatedBy,
  LocalCreatedAt,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;

  mapping for zdt_inct_h_bam
    {
      HisUuid            = his_uuid;
      IncUuid            = inc_uuid;
      HisId              = his_id;
      PreviousStatus     = previous_status;
      NewStatus          = new_status;
      Text               = text;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}