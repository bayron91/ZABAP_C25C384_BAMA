@Metadata.layer: #CORE
@UI:{ headerInfo:{ typeName: 'Incident',
                   typeNamePlural: 'Incidents',
                   title: { type: #STANDARD,
                            value: 'IncidentId' }},
                   presentationVariant: [{ sortOrder: [{ by: 'IncidentId',
                                                         direction: #ASC }],
                                           visualizations: [{ type: #AS_LINEITEM }]}]
}
annotate entity ZDD_C_INCIDENT_BAM with
{
  @UI.facet: [{ id: 'IncidentMasterData',
                purpose: #STANDARD,
                type: #IDENTIFICATION_REFERENCE,
                label: 'Incident Master Data',
                position: 10 }]
  @UI: { lineItem: [{ importance: #HIGH },
                    { type: #FOR_ACTION,
                      dataAction: 'ChangeStatus',
                      label: 'Change Status',
                      position: 10 }],
         identification: [{ type: #FOR_ACTION,
                            dataAction: 'ChangeStatus',
                            label: 'Change Status',
                            position: 10  }]}
  @UI.hidden: true
  IncUuid;

  @UI: { lineItem: [{ position: 10,
                      importance: #MEDIUM,
                      label: 'Incident ID' }],
         selectionField: [{ position: 10 }],
         identification: [{ position: 10 }]
  }
  IncidentId;

  @UI: { lineItem: [{ position: 20,
                      importance: #LOW }],
         identification: [{ position: 20 }]
  }
  Title;

  @UI: { lineItem: [{ position: 30,
                      importance: #LOW,
                      label: 'Incident Description' }],
         selectionField: [{ position: 30 }],
         identification: [{ position: 30 }]
  }
  Description;

  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZDD_STATUS_VH',
                                                 element: 'StatusCode' },
                                       useForValidation: true }]
  @UI: { lineItem: [{ position: 40,
                    importance: #LOW }],
       selectionField: [{ position: 40 }],
       identification: [{ position: 40 }]
  }
  Status;

  @UI.hidden: true
  StatusDescription;

  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZDD_PRIORITY_VH',
                                                 element: 'PriorityCode' },
                                       useForValidation: true }]
  @UI: { lineItem: [{ position: 50,
                  importance: #LOW }],
     selectionField: [{ position: 50 }],
     identification: [{ position: 50 }]
  }
  Priority;

  @UI.hidden: true
  PriorityDescription;

  @UI: { lineItem: [{ position: 60,
                  importance: #LOW }],
     selectionField: [{ position: 60 }],
     identification: [{ position: 60 }]
  }
  @Consumption.filter: { selectionType: #INTERVAL,
                         defaultValue: 'TODAY..TODAY' }
  CreationDate;

  @UI: { lineItem: [{ position: 70,
                importance: #LOW }],
   selectionField: [{ position: 70 }],
   identification: [{ position: 70 }]
  }
  ChangedDate;

  @UI.hidden: true
  LocalCreatedBy;
  @UI.hidden: true
  LocalCreatedAt;
  @UI.hidden: true
  LocalLastChangedBy;
  @UI.hidden: true
  LocalLastChangedAt;
  @UI.hidden: true
  LastChangedAt;

  /* Associations */
  @UI.hidden: true
  _History;
  @UI.hidden: true
  _Priority;
  @UI.hidden: true
  _Status;
}